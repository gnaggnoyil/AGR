# Maintainer: Yonggang Li <gnaggnoyil@gmail.com>
# Contributor: Victor <v1c70rp at gmail dot com>
# Contributor: Xiang Gao <qasdfgtyuiop@gmail.com>

_pkgbasename=thrust
pkgname=${_pkgbasename}-rapidsai
pkgver=1.17.2
pkgrel=1
pkgdesc='A C++ parallel programming library which resembles the C++ Standard Library (version following rapidsai)'
arch=('x86_64' 'aarch64')
url="https://github.com/thrust/${_pkgbasename}"
license=('APACHE')
makedepends=('cmake')
optdepends=(
    'cuda: for CUDA backend'
    'openmp: for OMP backend'
    'intel-tbb: for TBB backend'
)
source=(
    "${_pkgbasename}-${pkgver}.tar.gz::https://github.com/thrust/${_pkgbasename}/archive/${pkgver}.tar.gz"
    "cub-${pkgver}.tar.gz::https://github.com/thrust/cub/archive/${pkgver}.tar.gz"
)
sha256sums=('d021e37f5aac30fd1b9737865399feb57db8e601ae2fc0af3cd41784435e9523'
            '1013a595794548c359f22c07e1f8c620b97e3a577f7e8496d9407f74566a3e2a')

prepare() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}/dependencies"
    rm -d "cub"
    ln -s "${srcdir}/cub-${pkgver}" "${srcdir}/${_pkgbasename}-${pkgver}/dependencies/cub"
}

build() {
    _cmake_args=(
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_INSTALL_PREFIX="/opt/${pkgname}"

        # Testing requires to be run on machines that have compatible hardware. Plus, the requirements
        # for the hardware itself will probably increase depending on ctest args which might be able
        # to be set by the user or in the makepkg.conf. Therefore we disable tests here for now.
        -DTHRUST_ENABLE_HEADER_TESTING=OFF
        -DTHRUST_ENABLE_TESTING=OFF
        -DTHRUST_ENABLE_EXAMPLES=OFF
    )

    cd "${srcdir}"
    mkdir -p build
    cd "${srcdir}/build"
    cmake "${_cmake_args[@]}" "${srcdir}/${_pkgbasename}-${pkgver}"
    cmake --build . --config Release
}

package() {
    cd "${srcdir}/build"
    DESTDIR=${pkgdir} cmake --install . --config Release

    install -Dm644 "${srcdir}/${_pkgbasename}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
