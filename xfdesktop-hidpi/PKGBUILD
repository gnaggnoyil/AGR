# Maintainer: Yonggang Li <gnaggnoyil@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgbasename=xfdesktop
pkgname=$pkgbasename-hidpi
pkgver=4.16.0
pkgrel=2
pkgdesc="A desktop manager for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfdesktop/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck3' 'exo' 'thunar' 'garcon' 'hicolor-icon-theme')
makedepends=('intltool')
provides=("$pkgbasename")
conflicts=("$pkgbasename")
source=("https://archive.xfce.org/src/xfce/$pkgbasename/${pkgver%.*}/$pkgbasename-$pkgver.tar.bz2"
        "1-blur_background.diff")
sha256sums=('934ba5affecff21e62d9fac1dd50c50cd94b3a807fefa5f5bff59f3d6f155bae'
            '5773e3c130e6aa0294679e68bbd823549cbcecb83dd58271437a6b4128c18661')

prepare() {
  cd "$srcdir/$pkgbasename-$pkgver"
  patch -i "${srcdir}/1-blur_background.diff" -p1
}

build() {
  cd "$srcdir/$pkgbasename-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
