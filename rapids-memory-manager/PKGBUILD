# Maintainer: Yonggang Li <gnaggnoyil@gmail.com>

_pkgbasename=rmm
pkgname=rapids-memory-manager
pkgdesc='RAPIDS Memory Manager'
pkgver=23.12.00
pkgrel=1
arch=('x86_64')
url="https://github.com/rapidsai/${_pkgbasename}"
license=('Apache-2.0')
depends=(
    'cuda' # For cudart, cuda itself not needed.
    # See rapids-cmake/rapids-cmake/cpm/versions.json
    # The patches specified in `versions.json` needs to be added to those
    # dependency packages, since... that's how CPM works.
    'spdlog>=1.11.0'
    # rapids-cmake/rapids-cmake/cpm/thrust.cmake specifies `EXACT` for find_package(thrust)
    'thrust-rapidsai=1.17.2'
)
# It's difficult to fit needs of the python binding so we'll not build this for now
#_depends_python_rapids_rmm=(
#    'python>=3.8'
#    'python<3.10'
#    'python-cuda>=11.6'
#    'python-cuda<12.0'
#    'python-numpy'
#)
#_makedepends_python_rapids_rmm=(
#    'python-wheel'
#    'python-scikit-build>=0.13.1'
#    'cython>=0.29'
#    'cython<0.30'
#    'cmake>=3.20.1'
#    'cmake<3.23'
#    'ninja'
#)
# Git is needed anyway as rapids-cmake uses this to implement its PATCH_COMMAND
# even if we forces the use of already installed dependency packages.
makedepends=('cmake' 'git')
# This version number is specified in rapids-cmake/cpm/detail/download.cmake
_cpmver=0.38.5
source=(
    # Starting from v23.02.00 the branch-${_pkgbasever} branch no longer got unfreezed, and tags now
    # have a non-"a" version, both for rmm and for rapids-cmake. Plus v23.06.00 tag also have extra
    # unimportant commits mainly related to repo tagging and etc. Thus We think it is safe to use
    # corresponding tags now, even if rmm still specifies branch instead of tags for rapids-cmake
    "rapids-cmake-${pkgver}.tar.gz::https://github.com/rapidsai/rapids-cmake/archive/v${pkgver}.tar.gz"
    "${_pkgbasename}-${pkgver}.tar.gz::https://github.com/rapidsai/${_pkgbasename}/archive/v${pkgver}.tar.gz"
    "https://github.com/cpm-cmake/CPM.cmake/releases/download/v${_cpmver}/CPM.cmake"
    #"rapids-cmake-0001-correctly-find-thrust-version.patch"
    #"${_pkgbasename}-0001-avoid-uncessary-download.patch"
)
sha256sums=('81fe467e8b2b5a947c587ffefbea370b0140ab891f17e0e773a08aaee851b92d'
            '8a274613d9d114212d7cfd74a1082d551968487208a8ad5a84de33164623359a'
            '192aa0ccdc57dfe75bd9e4b176bf7fb5692fd2b3e3f7b09c74856fc39572b31c')

prepare() {
    #cd "${srcdir}/rapids-cmake-${_rapids_cmake_tag}"
    #patch -i "${srcdir}/rapids-cmake-0001-correctly-find-thrust-version.patch" -p1

    #cd "${srcdir}/${_pkgbasename}-${pkgver}"
    #patch -i "${srcdir}/${_pkgbasename}-0001-avoid-uncessary-download.patch" -p1

    cd "${srcdir}"
    mkdir -p build
    cd build
    ln -s "${srcdir}/rapids-cmake-${pkgver}/RAPIDS.cmake" "RMM_RAPIDS.cmake"
    mkdir -p cmake
    ln -s "${srcdir}/CPM.cmake" "cmake/CPM_${_cpmver}.cmake"
}

build() {
    _cmake_args=(
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_INSTALL_PREFIX=/usr

        -DThrust_ROOT=/opt/thrust-rapidsai
        -DFETCHCONTENT_SOURCE_DIR_RAPIDS-CMAKE="${srcdir}/rapids-cmake-${pkgver}"
        -DCPM_LOCAL_PACKAGES_ONLY=ON

        # Disable check operations since check operations for cuda programs are not so well-defined.
        -DBUILD_TESTS=OFF
        #-DBUILD_BENCHMARKS=OFF # Default is OFF
    )

    cd "${srcdir}/build"
    cmake "${_cmake_args[@]}" "${srcdir}/${_pkgbasename}-${pkgver}" # --log-level=VERBOSE
    cmake --build . --config Release
}

package() {
    cd "${srcdir}/build"
    DESTDIR=${pkgdir} cmake --install . --config Release

    install -Dm644 "${srcdir}/${_pkgbasename}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
