# Maintainer: Yonggang Li <gnaggnoyil@gmail.com>

_pkgbasename=rmm
pkgname=rapids-memory-manager
pkgdesc='RAPIDS Memory Manager'
pkgver=24.04.00
pkgrel=1
arch=('x86_64')
url="https://github.com/rapidsai/${_pkgbasename}"
license=('Apache-2.0')
depends=(
    'cuda' # For cudart, cuda itself not needed.
    # See rapids-cmake/rapids-cmake/cpm/versions.json
    # The patches specified in `versions.json` needs to be added to those
    # dependency packages, since... that's how CPM works.
    'spdlog>=1.12.0'
    # rapids-cmake/rapids-cmake/cpm/cccl.cmake specifies `EXACT` for find_package(cccl)
    'cccl-rapidsai=2.2.0'
)
# It's difficult to fit needs of the python binding so we'll not build this for now
#_depends_python_rapids_rmm=(
#    'python>=3.8'
#    'python<3.10'
#    'python-cuda>=11.6'
#    'python-cuda<12.0'
#    'python-numpy'
#)
#_makedepends_python_rapids_rmm=(
#    'python-wheel'
#    'python-scikit-build>=0.13.1'
#    'cython>=0.29'
#    'cython<0.30'
#    'cmake>=3.20.1'
#    'cmake<3.23'
#    'ninja'
#)
# Git is needed anyway as rapids-cmake uses this to implement its PATCH_COMMAND
# even if we forces the use of already installed dependency packages.
makedepends=('cmake' 'git')
# This version number is specified in rapids-cmake/cpm/detail/download.cmake
_cpmver=0.38.5
# In `fetch-rapids.cmake`
_rapids_cmake_ver=24.04.00
source=(
    # Starting from v23.02.00 the branch-${_pkgbasever} branch no longer got unfreezed, and tags now
    # have a non-"a" version, both for rmm and for rapids-cmake. Plus v23.06.00 tag also have extra
    # unimportant commits mainly related to repo tagging and etc. Thus We think it is safe to use
    # corresponding tags now, even if rmm still specifies branch instead of tags for rapids-cmake
    # Use rapids-cmake v24.02.01 instead of v24.02.00 since rmm 24.02.00 specifies `branch-24.02`
    # for rapids-cmake and as of date 2024-04-01 this branch more refers (or matches) tag v24.02.01
    # than v24.02.00
    # But in this specific case `v24.02.01` and `v24.02.00` should make no difference.
    "rapids-cmake-${_rapids_cmake_ver}.tar.gz::https://github.com/rapidsai/rapids-cmake/archive/v${_rapids_cmake_ver}.tar.gz"
    "${_pkgbasename}-${pkgver}.tar.gz::https://github.com/rapidsai/${_pkgbasename}/archive/v${pkgver}.tar.gz"
    "https://github.com/cpm-cmake/CPM.cmake/releases/download/v${_cpmver}/CPM.cmake"
)
sha256sums=('8dab8ae92e5a72e9167937c290ba63bc0680d371e72c72d9fcbc13e23c82ee44'
            'bb20877c8d92b322dbcb348c2009040784189d3d3c48f93011e13c1b34f6a22f'
            '192aa0ccdc57dfe75bd9e4b176bf7fb5692fd2b3e3f7b09c74856fc39572b31c')

prepare() {
    cd "${srcdir}"
    mkdir -p build
    cd build
    ln -s "${srcdir}/rapids-cmake-${_rapids_cmake_ver}/RAPIDS.cmake" "RMM_RAPIDS.cmake"
    mkdir -p cmake
    ln -s "${srcdir}/CPM.cmake" "cmake/CPM_${_cpmver}.cmake"
}

build() {
    _cmake_args=(
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_INSTALL_PREFIX=/usr

        -DCCCL_ROOT=/opt/cccl-rapidsai
        -DFETCHCONTENT_SOURCE_DIR_RAPIDS-CMAKE="${srcdir}/rapids-cmake-${_rapids_cmake_ver}"
        -DCPM_LOCAL_PACKAGES_ONLY=ON

        # Disable check operations since check operations for cuda programs are not so well-defined.
        -DBUILD_TESTS=OFF
        #-DBUILD_BENCHMARKS=OFF # Default is OFF
    )

    cd "${srcdir}/build"
    cmake "${_cmake_args[@]}" "${srcdir}/${_pkgbasename}-${pkgver}" # --log-level=VERBOSE
    cmake --build . --config Release
}

package() {
    cd "${srcdir}/build"
    DESTDIR=${pkgdir} cmake --install . --config Release

    install -Dm644 "${srcdir}/${_pkgbasename}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
