# Maintainer: Yonggang Li <gnaggnoyil@gmail.com>

_pkgbasename=rmm
_pkgbasever=23.02
pkgname=rapids-memory-manager
pkgdesc='RAPIDS Memory Manager'
pkgver=${_pkgbasever}.00
pkgrel=1
arch=('x86_64')
url="https://github.com/rapidsai/${_pkgbasename}"
license=('APACHE')
depends=(
    'cuda' # For cudart, cuda itself not needed.
    # See rapids-cmake/rapids-cmake/cpm/versions.json
    # The patches specified in `versions.json` needs to be added to those
    # dependency packages, since... that how CPM works.
    'spdlog>=1.8.5'
    # rapids-cmake/rapids-cmake/cpm/thrust.cmake specifies `EXACT` for find_package(thrust)
    'thrust-rapidsai=1.17.2'
)
# It's difficult to fit needs of the python binding so we'll not build this for now
#_depends_python_rapids_rmm=(
#    'python>=3.8'
#    'python<3.10'
#    'python-cuda>=11.6'
#    'python-cuda<12.0'
#    'python-numpy'
#)
#_makedepends_python_rapids_rmm=(
#    'python-wheel'
#    'python-scikit-build>=0.13.1'
#    'cython>=0.29'
#    'cython<0.30'
#    'cmake>=3.20.1'
#    'cmake<3.23'
#    'ninja'
#)
# Git is needed anyway as rapids-cmake uses this to implement its PATCH_COMMAND
# even if we forces the use of already installed dependency packages.
makedepends=('cmake' 'git')
_rapids_cmake_tag='c7a28304639a2ed460181b4753f3280c7833c718'
# This version number is specified in rapids-cmake/cpm/detail/download.cmake
_cpmver=0.35.6
source=(
    # RMM source specifies branch-${_pkgbasever} for rapids-cmake. Given that tag/v${_pkgbasever}.00a
    # and branch-${_pkgbasever} are actuall different, we do the following to ensure a reproducible build:
    # * Always choose the latest commit of branch-${_pkgbasever} when we started to bump version, and
    #   never changes the commit id otherwise.
    "rapids-cmake-${_rapids_cmake_tag}.tar.gz::https://github.com/rapidsai/rapids-cmake/archive/${_rapids_cmake_tag}.tar.gz"
    "${_pkgbasename}-${pkgver}.tar.gz::https://github.com/rapidsai/${_pkgbasename}/archive/v${pkgver}.tar.gz"
    "https://github.com/cpm-cmake/CPM.cmake/releases/download/v${_cpmver}/CPM.cmake"
    #"rapids-cmake-0001-correctly-find-thrust-version.patch"
    #"${_pkgbasename}-0001-avoid-uncessary-download.patch"
)
sha256sums=('ac5c6764e0a11fbfb7d84ff2fc602abe644c244ae6abfc024851aad35096fa97'
            '15e05d1aed4efc7461b159b1054c5b3026b0b7f5ed8ed40a991dde0e7a024da6'
            '6b47e2a94b69c59acda5567401afc67ac53e9a95bf90cfd4f1e9792c4d8ec815')

prepare() {
    #cd "${srcdir}/rapids-cmake-${_rapids_cmake_tag}"
    #patch -i "${srcdir}/rapids-cmake-0001-correctly-find-thrust-version.patch" -p1

    #cd "${srcdir}/${_pkgbasename}-${pkgver}"
    #patch -i "${srcdir}/${_pkgbasename}-0001-avoid-uncessary-download.patch" -p1

    cd "${srcdir}"
    mkdir -p build
    cd build
    ln -s "${srcdir}/rapids-cmake-${_rapids_cmake_tag}/RAPIDS.cmake" "RMM_RAPIDS.cmake"
    mkdir -p cmake
    ln -s "${srcdir}/CPM.cmake" "cmake/CPM_${_cpmver}.cmake"
}

build() {
    _cmake_args=(
        -DCMAKE_BUILD_TYPE=Release
        -DCMAKE_INSTALL_PREFIX=/usr

        -DThrust_ROOT=/opt/thrust-rapidsai
        -DFETCHCONTENT_SOURCE_DIR_RAPIDS-CMAKE="${srcdir}/rapids-cmake-${_rapids_cmake_tag}"
        -DCPM_LOCAL_PACKAGES_ONLY=ON

        # Disable check operations since check operations for cuda programs are not so well-defined.
        -DBUILD_TESTS=OFF
        #-DBUILD_BENCHMARKS=OFF # Default is OFF
    )

    cd "${srcdir}/build"
    cmake "${_cmake_args[@]}" "${srcdir}/${_pkgbasename}-${pkgver}" # --log-level=VERBOSE
    cmake --build . --config Release
}

package() {
    cd "${srcdir}/build"
    DESTDIR=${pkgdir} cmake --install . --config Release

    install -Dm644 "${srcdir}/${_pkgbasename}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
